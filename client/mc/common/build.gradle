import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id('java')
}

group = "calebxzhou.rdi.mc.common"
version = "0.1"

repositories {
    mavenLocal()
    mavenCentral()
}
dependencies {

}
java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(8)

    options.compilerArgs << "-Xlint:-deprecation"
    options.compilerArgs << "-Xlint:-removal"
}
static void registerClientCopyTask(String taskName, Project project, List<String> extraDestinations = []) {
    project.tasks.register(taskName) {
        dependsOn project.tasks.named('build')
        def artifact = project.layout.buildDirectory.file("libs/rdi-${project.version}.jar")
        def baseDestinations = [
                project.layout.projectDirectory.dir('..\\..\\..\\server\\master\\run\\client-libs'),
                project.layout.projectDirectory.dir('..\\..\\ui\\run\\download-mods')
        ]
        def destinationDirs = baseDestinations + extraDestinations.collect { project.layout.projectDirectory.dir(it) }

        doLast {
            def jarFile = artifact.get().asFile
            if (!jarFile.exists()) {
                throw new GradleException("未找到构建产物: ${jarFile}")
            }
            destinationDirs.forEach { target ->
                def targetDir = target.asFile
                targetDir.mkdirs()
                def destFile = new File(targetDir, jarFile.name)
                Files.copy(
                        jarFile.toPath(),
                        destFile.toPath(),
                        StandardCopyOption.REPLACE_EXISTING
                )
            }
        }
    }
}
ext.registerClientCopyTask = { Project targetProject ->
    registerClientCopyTask("出core-local", targetProject,[])
    registerClientCopyTask("出core-release", targetProject,["\\\\rdi5\\rdi55\\ihq\\client-libs\\"])
}