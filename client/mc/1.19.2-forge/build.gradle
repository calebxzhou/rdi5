plugins {
    id 'net.neoforged.moddev.legacyforge' version '2.0.124'
}

// Ensure the common project is configured before we read its outputs
evaluationDependsOn(":c-mc-common")

base {
    archivesName = "rdi"
}

version = "5-mc-client-${minecraft_version}-forge"
group = "calebxzhou.rdi"

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}
sourceSets.main {
    ext["refMap"] = "rdi.mixins.refmap.json"
}
mixin {
    add sourceSets.main, 'rdi.mixins.refmap.json'
    config "rdi.mixins.json"
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = "Forge Maven"
        url = "https://maven.minecraftforge.net/"
    }

}
legacyForge {
    version = "${minecraft_version}-${forge_version}"
    runs {
        client {
            client()
            systemProperty("rdi.ihq.url", "http://127.0.0.1:65231")
            systemProperty("rdi.game.ip", "127.0.0.1:65230")
            systemProperty("rdi.host.name", "测试测试12123大世界")
            systemProperty("rdi.host.port", "25565")
            systemProperty("mixin.hotSwap", "true")
            programArguments = [
                    "-mixin.config=rdi.mixins.json",
                    "--username",
                    "哇塞的哇塞的",
                    "--uuid",
                    "68b314bbadaf52ddab96b5ed00000000",
                    "--width",
                    "2560",
                    "--height",
                    "1440"
            ]
            gameDirectory = project.file('run')
        }
    }
    mods {
        rdi {

            sourceSet sourceSets.main

        }
    }
    parchment {
        // Get versions from https://parchmentmc.org/docs/getting-started
        // Omit the "v"-prefix in mappingsVersion
        minecraftVersion = minecraft_version
        mappingsVersion = parchment_mappings
    }
}

dependencies {
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    implementation(project(":c-mc-common"))
    additionalRuntimeClasspath(project(":c-mc-common"))
    //jarjar(project(":c-mc-common"))
}

processResources {
    var properties = [
            "mod_license"      : "gplv3",
            "mod_id"           : mod_id,
            "version"          : version,
            "mod_name"         : mod_name,
            "mod_author"       : mod_authors,
            "forge_version"    : forge_version,
            "minecraft_version": minecraft_version,
    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(17)

    options.compilerArgs << "-Xlint:-deprecation"
    options.compilerArgs << "-Xlint:-removal"
}

java {
    withSourcesJar()
}

jar {
    // Bundle compiled outputs from :c-mc-common into this jar
    dependsOn project(":c-mc-common").tasks.named("classes")
    from(project(":c-mc-common").sourceSets.main.output)

    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "rdi.mixins.json"
        ])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

project(':c-mc-common').ext.registerClientCopyTask(project)