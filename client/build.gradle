plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.115'
    // Adds the Kotlin Gradle plugin
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    // OPTIONAL Kotlin Serialization plugin
    id 'org.jetbrains.kotlin.plugin.serialization' version "${kotlin_version}"
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name 'IzzelAliz Maven'
                url 'https://maven.izzel.io/releases/'
            }
        }
        filter {
            includeGroup "icyllis.modernui"
        }
    }
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')
    validateAccessTransformers = true
    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()
            programArgument '--width'
            programArgument '2560'
            programArgument '--height'
            programArgument '1440'
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
            systemProperty 'rdi.debug', "true"
            systemProperty 'rdi.noUpdate', "true"

        }
       /* server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }
*/
        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
    libraries
    // This will make sure that all dependencies that you add to the libraries configuration will also be added to the implementation configuration
    // This way, you only need one dependency declaration for both runtime and compile dependencies
    implementation.extendsFrom libraries
}

configurations.libraries {
    transitive = true
}
allprojects {
    configurations.all {
        resolutionStrategy {
            force("org.slf4j:slf4j-api:2.0.16")
        }
    }
}
//三方库
def mcLibs = [
// https://mvnrepository.com/artifact/org.jsoup/jsoup
//添加三方库别忘了jar jarJar reobfJarJar genIntellijRuns 不然找不到class
"io.ktor:ktor-client-okhttp:$ktor_version",
"io.ktor:ktor-client-core:$ktor_version",
"io.ktor:ktor-client-auth:$ktor_version",
"io.ktor:ktor-client-encoding:$ktor_version",
'org.jsoup:jsoup:1.19.1',
'org.mongodb:bson:5.1.0',
// https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
'com.github.ben-manes.caffeine:caffeine:3.2.2',
"io.ktor:ktor-server-core:$ktor_version",
"io.ktor:ktor-server-jetty-jakarta:$ktor_version",
'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0',
'org.jetbrains.kotlinx:kotlinx-serialization-cbor:1.9.0',
"io.ktor:ktor-client-content-negotiation:$ktor_version",
"io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
]
def mcLibExportOnly = [
]
def modrinthMods = [
        "justenoughcharacters:4.5.15",
        "notenoughcrashes:4.4.8+1.21.1-neoforge",
        "imblocker-original:5.0.2",
        "sodium:mc1.21.1-0.6.13-neoforge"
]
def modrinthModsCompileOnly = [
]
def curseMods = [
]
def curseExportOnly = [
]
dependencies {
    // Example optional mod dependency with JEI
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    // compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    // We add the full version to localRuntime, not runtimeOnly, so that we do not publish a dependency on it
    // localRuntime "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation "blank:coolmod-${mc_version}:${coolmod_version}"

    // Example mod dependency using a file as dependency
    // implementation files("libs/coolmod-${mc_version}-${coolmod_version}.jar")

    // Example project dependency using a sister or child project:
    // implementation project(":myproject")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
    /**/
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    // https://mvnrepository.com/artifact/io.netty/netty-all
    testImplementation 'io.netty:netty-all:4.1.97.Final'

    def lwjglVersion = '3.3.3'
    def components = ['', 'glfw', 'opengl', 'openal', 'stb', 'tinyfd', 'jemalloc',]

    components.forEach { component ->
        testImplementation "org.lwjgl:lwjgl${component ? "-$component" : ''}:$lwjglVersion"
        testImplementation "org.lwjgl:lwjgl${component ? "-$component" : ''}:$lwjglVersion:natives-windows"
    }
    implementation('org.hotswapagent:hotswap-agent-core:2.0.1')
    //implementation 'thedarkcolour:kotlinforforge-neoforge:5.7.0:slim'
    //implementation 'thedarkcolour:kotlinforforge-neoforge:5.10.0'
    implementation 'thedarkcolour:kfflang-neoforge:5.10.0'
    implementation 'thedarkcolour:kfflib-neoforge:5.10.0'
    implementation 'thedarkcolour:kffmod-neoforge:5.10.0'
    //mui
    implementation 'icyllis.modernui:ModernUI-Core:3.12.0'
    testImplementation 'icyllis.modernui:ModernUI-Core:3.12.0'
    additionalRuntimeClasspath 'icyllis.modernui:ModernUI-Core:3.12.0'
    implementation 'icyllis.modernui:ModernUI-NeoForge:1.21.1-3.12.0.2'
    implementation "icyllis.modernui:ModernUI-Markflow:3.12.0"
    additionalRuntimeClasspath "icyllis.modernui:ModernUI-Markflow:3.12.0"
    testImplementation "icyllis.modernui:ModernUI-Markflow:3.12.0"

    // compile against the JEI API but do not include it at runtime
    compileOnly("mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}")
    // at runtime, use the full JEI jar for NeoForge
    runtimeOnly("mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}")

// https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    testImplementation("ch.qos.logback:logback-classic:1.5.21")
    //循环添加deps
    def jarJarVisited = new LinkedHashSet<String>()
    //mc已经自带了 不需要重复导出
    def jarJarExcludeCoords = [
            'org.slf4j:slf4j-api',
            'com.google.errorprone:error_prone_annotations',

            'org.ow2.asm:asm',
            'org.ow2.asm:asm-commons',
            'org.ow2.asm:asm-tree',
            'org.ow2.asm:asm-analysis',
            'org.ow2.asm:asm-util',
    ] as Set
    def includeJarJarTransitives
    includeJarJarTransitives = { String notation ->
        def coordinateParts = notation.split(':')
        def coordinateKey = coordinateParts.length >= 2 ? "${coordinateParts[0]}:${coordinateParts[1]}" : notation
        if (jarJarExcludeCoords.iterator().findIndexOf { it == coordinateKey } >= 0) {

            println(coordinateKey+"不要")
            return
        }
        if (!jarJarVisited.add(notation)) {
            println(coordinateKey+"有了")
            return
        }
        println(coordinateKey+"要")
        jarJar(notation) {
            transitive = false
        }

        def dependency = project.dependencies.create(notation)
        def configuration = project.configurations.detachedConfiguration(dependency)
        configuration.transitive = true

        configuration.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            if ("jar".equalsIgnoreCase(artifact.type)) {
                def moduleId = artifact.moduleVersion.id
                def classifierSuffix = artifact.classifier ? ":${artifact.classifier}" : ""
                def artifactKey = "${moduleId.group}:${artifact.name}"
                if (jarJarExcludeCoords.contains(artifactKey)) {
                    return
                }
                def artifactNotation = "${moduleId.group}:${artifact.name}:${moduleId.version}${classifierSuffix}"
                includeJarJarTransitives(artifactNotation)
            }
        }
    }


    mcLibs.each { lib ->
        libraries(lib)
        includeJarJarTransitives(lib)
        additionalRuntimeClasspath lib
    }
    modrinthMods.each { dep ->
        implementation("maven.modrinth:$dep")

    }
    modrinthModsCompileOnly.each { dep ->
        compileOnly("maven.modrinth:$dep")
    }
    curseMods.each { dep ->
        implementation "curse.maven:$dep-deobf"
    }
    curseExportOnly.each { dep ->
    }
    //for modernui
    configurations.configureEach {
        resolutionStrategy {
            force 'com.ibm.icu:icu4j:76.1'
            force 'it.unimi.dsi:fastutil:8.5.15'
        }
    }
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation


}
test {
    useJUnitPlatform()
}
// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

tasks.register('出core', Copy) {
    dependsOn(tasks.named('build'))
    from(layout.buildDirectory.file("libs/rdi-${version}.jar"))
    into("..\\ihq\\run")
}