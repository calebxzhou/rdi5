import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'net.neoforged.moddev' version '2.0.124'
    // Adds the Kotlin Gradle plugin
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    // OPTIONAL Kotlin Serialization plugin
    id 'org.jetbrains.kotlin.plugin.serialization' version "${kotlin_version}"
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    } 
}

base {
    archivesName = mod_id
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = project.neo_version

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers = project.files('src/main/resources/META-INF/accesstransformer.cfg')
    validateAccessTransformers = true
    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()
            programArgument '--width'
            programArgument '2560'
            programArgument '--height'
            programArgument '1440'
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
            systemProperty 'rdi.ihq.url', "http://127.0.0.1:65231"
            systemProperty 'rdi.game.ip', "127.0.0.1:65232"
            systemProperty 'rdi.host.name', "测试测试12123主机"
            systemProperty 'rdi.host.port', "55000"

        }


        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime
    libraries
    // This will make sure that all dependencies that you add to the libraries configuration will also be added to the implementation configuration
    // This way, you only need one dependency declaration for both runtime and compile dependencies
    implementation.extendsFrom libraries
}

configurations.libraries {
    transitive = true
}
allprojects {
    configurations.all {
        resolutionStrategy {
            force("org.slf4j:slf4j-api:2.0.16")
        }
    }
}
//三方库
def mcLibs = [
// https://mvnrepository.com/artifact/org.jsoup/jsoup
//添加三方库别忘了jar jarJar reobfJarJar genIntellijRuns 不然找不到class
"io.ktor:ktor-client-okhttp:$ktor_version",
"io.ktor:ktor-client-core:$ktor_version",
"io.ktor:ktor-client-websockets:$ktor_version",
"io.ktor:ktor-client-encoding:$ktor_version",
'org.jsoup:jsoup:1.19.1',
'org.mongodb:bson:5.1.0',
// https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
'com.github.ben-manes.caffeine:caffeine:3.2.2',
'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0',
"io.ktor:ktor-serialization-kotlinx-json:$ktor_version"
]
def modrinthMods = [
        "justenoughcharacters:4.5.15",
        "notenoughcrashes:4.4.8+1.21.1-neoforge",
        "imblocker-original:5.0.2",
        "sodium:mc1.21.1-0.6.13-neoforge"
]
def curseMods = [
]
dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
// https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-reflect
    implementation('org.hotswapagent:hotswap-agent-core:2.0.1')
    //runtimeOnly 'thedarkcolour:kotlinforforge-neoforge:5.10.0'
    /*implementation 'thedarkcolour:kfflang-neoforge:5.10.0'
    implementation 'thedarkcolour:kfflib-neoforge:5.10.0'
    implementation 'thedarkcolour:kffmod-neoforge:5.10.0'*/

    testImplementation("ch.qos.logback:logback-classic:1.5.21")
    //循环添加deps
    def jarJarVisited = new LinkedHashSet<String>()
    //mc已经自带了 不需要重复导出
    def jarJarExcludeCoords = [
            'org.slf4j:slf4j-api',
            'com.google.errorprone:error_prone_annotations',
            'org.ow2.asm:asm',
            'org.ow2.asm:asm-commons',
            'org.ow2.asm:asm-tree',
            'org.ow2.asm:asm-analysis',
            'org.ow2.asm:asm-util',
    ] as Set
    def includeJarJarTransitives
    includeJarJarTransitives = { String notation ->
        def coordinateParts = notation.split(':')
        def coordinateKey = coordinateParts.length >= 2 ? "${coordinateParts[0]}:${coordinateParts[1]}" : notation
        if (jarJarExcludeCoords.iterator().findIndexOf { it == coordinateKey } >= 0) {

            println(coordinateKey+"不要")
            return
        }
        if (!jarJarVisited.add(notation)) {
            println(coordinateKey+"有了")
            return
        }
        println(coordinateKey+"要")
        jarJar(notation) {
            transitive = false
        }

        def dependency = project.dependencies.create(notation)
        def configuration = project.configurations.detachedConfiguration(dependency)
        configuration.transitive = true

        configuration.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            if ("jar".equalsIgnoreCase(artifact.type)) {
                def moduleId = artifact.moduleVersion.id
                def classifierSuffix = artifact.classifier ? ":${artifact.classifier}" : ""
                def artifactKey = "${moduleId.group}:${artifact.name}"
                if (jarJarExcludeCoords.contains(artifactKey)) {
                    return
                }
                def artifactNotation = "${moduleId.group}:${artifact.name}:${moduleId.version}${classifierSuffix}"
                includeJarJarTransitives(artifactNotation)
            }
        }
    }


    mcLibs.each { lib ->
        libraries(lib)
        includeJarJarTransitives(lib)
        additionalRuntimeClasspath lib
    }
    modrinthMods.each { dep ->
        implementation("maven.modrinth:$dep")

    }
    curseMods.each { dep ->
        implementation "curse.maven:$dep-deobf"
    }

}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : neo_version,
            neo_version_range      : neo_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation


}
test {
    useJUnitPlatform()
}
// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
tasks.named('test') {
    enabled = false
}
tasks.register('出core-debug') {
    dependsOn(tasks.named('build'))
    def artifact = layout.buildDirectory.file("libs/rdi-${version}.jar")
    def destinations = [
        layout.projectDirectory.dir("..${File.separator}ihq${File.separator}run"),
        layout.projectDirectory.dir("${System.getProperty('user.home')}\\Documents\\RDI5sea-Ref\\.minecraft\\versions\\RDI5.5\\mods")
    ]

    doLast {
        def jarFile = artifact.get().asFile
        if (!jarFile.exists()) {
            throw new GradleException("未找到构建产物: ${jarFile}")
        }
        destinations.each { target ->
            def targetDir = target.asFile
            targetDir.mkdirs()
            def destFile = new File(targetDir, jarFile.name)
            Files.copy(
                jarFile.toPath(), 
                destFile.toPath(),
                    StandardCopyOption.REPLACE_EXISTING
            )
        }
    }
}
tasks.register('出core-release') {
    dependsOn(tasks.named('build'))
    def artifact = layout.buildDirectory.file("libs/rdi-${version}.jar")
    def destinations = [
        layout.projectDirectory.dir("..${File.separator}ihq${File.separator}run"),
        layout.projectDirectory.dir("${System.getProperty('user.home')}\\Documents\\RDI5sea-Ref\\.minecraft\\versions\\RDI5.5\\mods"),
        layout.projectDirectory.dir("\\\\rdi5\\rdi55\\ihq")
    ]


    doLast {
        def jarFile = artifact.get().asFile
        if (!jarFile.exists()) {
            throw new GradleException("未找到构建产物: ${jarFile}")
        }
        destinations.each { target ->
            def targetDir = target.asFile
            targetDir.mkdirs()
            def destFile = new File(targetDir, jarFile.name)
            java.nio.file.Files.copy(
                jarFile.toPath(), 
                destFile.toPath(),
                java.nio.file.StandardCopyOption.REPLACE_EXISTING
            )
        }
    }
}