plugins {
    id 'net.neoforged.moddev.legacyforge' version '2.0.124'
}

base {
    archivesName = "rdi"
}

version = "5-mc-server-1.20.1-forge"
group = "calebxzhou.rdi"

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}
sourceSets.main {
    ext["refMap"] = "rdi.mixins.refmap.json"
}
mixin {
    add sourceSets.main, 'rdi.mixins.refmap.json'
    config "rdi.mixins.json"
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        name = "Forge Maven"
        url = "https://maven.minecraftforge.net/"
    }

}
legacyForge {
    version = "1.20.1-47.4.13"
    runs {
        server {
            server()
            systemProperty("rdi.ihq.url", "127.0.0.1:65231")
            systemProperty("rdi.host.id", "6953e0bccb51b879288e19a3")
            systemProperty("mixin.hotSwap", "true")
            programArguments = [
                "--nogui",
                    "-mixin.config=rdi.mixins.json",
            ]
        }
    }
    mods {
        rdi {

            sourceSet sourceSets.main

        }
    }
    parchment {
        // Get versions from https://parchmentmc.org/docs/getting-started
        // Omit the "v"-prefix in mappingsVersion
        minecraftVersion = "1.20.1"
        mappingsVersion = "2023.09.03"
    }
}

dependencies {
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"
    implementation project(':s-mc-common')
    additionalRuntimeClasspath project(':s-mc-common')
}

// Ensure the common project is evaluated before we look up its tasks/outputs
evaluationDependsOn(':s-mc-common')

processResources {
    var properties = [
            "mod_license"      : "gplv3",
            "mod_id"           : mod_id,
            "version"          : version,
            "mod_name"         : mod_name,
            "mod_author"       : mod_authors,
            "forge_version"    : forge_version,
            "minecraft_version": minecraft_version,
    ]
    inputs.properties(properties)

    filesMatching("META-INF/mods.toml") {
        expand properties
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release.set(17)

    options.compilerArgs << "-Xlint:-deprecation"
    options.compilerArgs << "-Xlint:-removal"
}

java {
    withSourcesJar()
}

jar {
    // Bundle compiled outputs from :c-mc-common into this jar
    dependsOn project(":s-mc-common").tasks.named("classes")
    from(project(":s-mc-common").sourceSets.main.output)
    manifest {
        attributes([
                "Specification-Title"     : mod_id,
                "Specification-Vendor"    : mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(GroovyCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}
evaluationDependsOn(':s-mc-common')
project(':s-mc-common').ext.registerClientCopyTask(project)
