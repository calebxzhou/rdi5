import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    id('java')
}

group = "calebxzhou.rdi.mc.common"
version = "0.1"

repositories {
    mavenLocal()
    mavenCentral()
}
dependencies {
// https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-api
    implementation 'org.apache.logging.log4j:log4j-api:2.20.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'com.neovisionaries:nv-websocket-client:2.14'
}
java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}
static void registerServerCopyTask(String taskName, Project project, Boolean release) {
    project.tasks.register(taskName) {
        dependsOn project.tasks.named('build')
        def mcVersionSlug = project.version.toString().replaceAll("5-mc-server-","")
        def artifact = project.layout.buildDirectory.file("libs/rdi-${project.version}.jar")
        def baseDestinations = [
                project.layout.projectDirectory.dir("..\\..\\master\\run\\game-libs\\${mcVersionSlug}\\mods")
        ]
        if (release) {
            baseDestinations += project.layout.projectDirectory.dir("\\\\rdi\\rdi55\\ihq\\game-libs\\${mcVersionSlug}\\mods")
        }
        def destinationDirs = baseDestinations

        doLast {
            def jarFile = artifact.get().asFile
            if (!jarFile.exists()) {
                throw new GradleException("未找到构建产物: ${jarFile}")
            }
            destinationDirs.forEach { target ->
                def targetDir = target.asFile
                targetDir.mkdirs()
                def destFile = new File(targetDir, jarFile.name)
                Files.copy(
                        jarFile.toPath(),
                        destFile.toPath(),
                        StandardCopyOption.REPLACE_EXISTING
                )
            }
        }
    }
}
ext.registerServerCopyTask = { Project targetProject ->
    registerServerCopyTask("出core-local", targetProject,false)
    registerServerCopyTask("出core-release", targetProject,true)
}
